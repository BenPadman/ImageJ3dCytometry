macro "Batch generate visual representations of 3D ROIs[F9]"{
/* 
 * ***MACRO DESCRIPTION***
 * The macro was written by Dr Benjamin Scott Padman, to enable the direct visualization of the 3D ROIs generated by his 3D OBIA scripts (MitoQuant_OBIA.ijm, FociQuant_OBIA.ijm) 
 * 
 * 
 * ***HOW TO USE THE MACRO***
 * STEP 1: You need to have already run one (or both) of the 3D quantification macros (MitoQuant_OBIA.ijm, FociQuant_OBIA.ijm).
 * If you followed the instructions included in those macros, your folder structure should look like this:
 * 	ROOTDIRECTORY/
 * 	├── EXPERIMENT (MAINFOLDER)/
 * 	    ├── Images 		(Containing the original 3D optical data)	
 * 	    ├── ROIs		(Containing the 3d ROI files)
 * 	    └── Outputs		(Containing the numerical outputs from OBIA)
 * 
 * STEP 2: Run this current macro, and it will ask you for 2 folders. Here is the purpose of each folder
 * FOLDER 1 - Select the directory containing all the original images
 * FOLDER 2 - Select the directory containing the 3D ROI files generated by one of the OBIA macros
 * 
 * STEP 3: Let the macro run and be patient.
 * The macro will load each individual 3D ROI file and create an image displaying the segmentations.
 * These visual maps will be output into a newly generated directory located here:
 * 	ROOTDIRECTORY/
 * 	├── EXPERIMENT (MAINFOLDER)/
 * 	    ├── 3D_ROI_INSPECTIONS 		***The New ROI images are stored here***
 * 	    ├── Images 			
 * 	    ├── ROIs		
 * 	    └── Outputs		
 * 
 * 	***DISCLAIMER*** 
 * This ImageJ macro is provided "As is". In no event shall the author of this macro or its contributors be liable for any direct, indirect, incidental, special, exemplary, or consequential damages (including, but not limited to, procurement of substitute goods or services; loss of use, data, or profits).
 */
dir = getDirectory("DIRECTORY WITH ORIGINAL IMAGES TO ANALYSE");
   roidir = getDirectory("CORRESPONDING ROI FILE DIRECTORY");
//      dir2 = getDirectory("OUTPUT DIRECTORY TO VISUALIZE ROIS");
dir2=File.getParent(roidir)+File.separator+"3D_ROI_INSPECTIONS";
if (!File.exists(dir2)) {
	   File.makeDirectory(dir2);
   } 

// 	***BATCH PROCESS INITIATED HERE***
   run("3D Manager");
   count = 0;
   countFiles(dir);
   n = 0;
   processFiles(dir);
run("Set Measurements...", "area mean standard modal min integrated median stack add redirect=None decimal=3");
   
   function countFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              countFiles(""+dir+list[i]);
          else
              count++;
      }
  }
   function processFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              processFiles(""+dir+list[i]);
          else {
             showProgress(n++, count);
             path = dir+list[i];
             processFile(path);
          }
      }
  }
  function processFile(path) {
  	           open(path);
  		imgname = getTitle();
   run("3D Manager");
		getDimensions(width, height, channels, slices, frames);
		run("Close All");
	if (File.exists(roidir+imgname+"_Foci3Droi.zip")) {
				newImage("BlankCanvas", "8-bit black", width, height, slices);
	selectWindow("BlankCanvas");
Ext.Manager3D_Load(roidir+imgname+"_Foci3Droi.zip");
Ext.Manager3D_SelectAll();
Ext.Manager3D_FillStack(255, 255, 255);
Ext.Manager3D_Segment(128, 255);
selectWindow("BlankCanvas");
run("Close");
selectWindow("BlankCanvas-3Dseg");
//run("3-3-2 RGB");
run("glasbey on dark");
saveAs("Tiff", dir2+File.separator+imgname+"_FociROIs.tif");
Ext.Manager3D_Reset();
		run("Close All");
   } else {
   	run("Close All");
   }
	if (File.exists(roidir+File.separator+imgname+"_Mito3Droi.zip")) {
				newImage("BlankCanvas", "8-bit black", width, height, slices);
	selectWindow("BlankCanvas");
Ext.Manager3D_Load(roidir+imgname+"_Mito3Droi.zip");
Ext.Manager3D_SelectAll();
Ext.Manager3D_FillStack(255, 255, 255);
Ext.Manager3D_Segment(128, 255);
selectWindow("BlankCanvas");
run("Close");
selectWindow("BlankCanvas-3Dseg");
//run("3-3-2 RGB");
run("glasbey on dark");
saveAs("Tiff", dir2+File.separator+imgname+"_MitoROIs.tif");
Ext.Manager3D_Reset();
		run("Close All");
   } else {
   	run("Close All");
   }		
}   
}
